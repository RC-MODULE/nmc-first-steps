global _ReverseBits: label;
data ".data"	      // весовые коэффициенты для перестановки битов
    Weights: long[64] = (   1l<<63, 1l<<61, 1l<<59, 1l<<57,
                            1l<<55, 1l<<53, 1l<<51, 1l<<49,
                            1l<<47, 1l<<45, 1l<<43, 1l<<41,
                            1l<<39, 1l<<37, 1l<<35, 1l<<33,
                            1l<<31, 1l<<29, 1l<<27, 1l<<25,
                            1l<<23, 1l<<21, 1l<<19, 1l<<17,
                            1l<<15, 1l<<13, 1l<<11, 1l<< 9,
                            1l<< 7, 1l<< 5, 1l<< 3, 1l<< 1,
                            1l<<62, 1l<<60, 1l<<58, 1l<<56,
                            1l<<54, 1l<<52, 1l<<50, 1l<<48,
                            1l<<46, 1l<<44, 1l<<42, 1l<<40,
                            1l<<38, 1l<<36, 1l<<34, 1l<<32,
                            1l<<30, 1l<<28, 1l<<26, 1l<<24,
                            1l<<22, 1l<<20, 1l<<18, 1l<<16,
                            1l<<14, 1l<<12, 1l<<10, 1l<< 8,
                            1l<< 6, 1l<< 4, 1l<< 2, 1l<< 0);
end ".data";	

begin ".text"	
<_ReverseBits>	
	ar5 = sp - 2;	
	push ar0, gr0;	
		
	ar0 = Weights;	  //в ar0 загружается адрес массива весов для матрицы ВП 
			
	nb1 = 0FFFFFFFFh; // 64 столбца
	sb  = 0FFFFFFFFh; // 32 строки
		
	// загрузка первого набора весовых коэффициентов в рабочую матрицу
	rep 32 wfifo = [ar0++],ftw, wtw;
	// загрузка второго набора весовых коэффициентов в теневую матрицу
	rep 32 wfifo = [ar0++],ftw;
	
    // слово входных данных напрямую из стека загружается в ram и одновременно подаётся 
    // на вход Х матричного устройства умножения
    rep 1 ram = [--ar5] with vsum , data, 0;
    wtw;
    // после обновления коэффициентов тоже слово входных данных подаётся из ram 
    // на вход Х, а по пути циклически сдвигается на 1 бит вправо 
    rep 1 with vsum , shift ram, afifo;
    rep 1 [ar5] = afifo;
	
	gr7 = [ar5++];	  //младшее слово вектора результата - в gr7
	gr6 = [ar5++];	  //старшее слово вектора результата - в gr6
	
	pop ar0, gr0;	
	return;	
		
end ".text";	
