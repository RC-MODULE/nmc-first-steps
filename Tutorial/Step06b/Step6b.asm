global __main: label; // объявление глобальной метки.
	
data ".MyData"        // секция инициализированных данных
    // массив А заполняется единицами
    A: word[64] = (1 dup 64);
    // вектор, используемый для суммирования строк матрицы весов
    Vect: long = 5555555555555555hl;
end ".MyData";	
	
nobits ".MyData1"	
    Temp: long;       // временный буфер для хранения промежуточных
                      // результатов
end ".MyData1";	
	
begin ".textAAA"      // начало секции кода
<__main>	
    ar1 = A;	
    nb1 = 80000000h;  // матрица делится на два столбца
    sb  = 0AAAAAAAAh; // и на 32 строки
		
    // элементы массива A загружаются в буфер wfifo
    rep 32 wfifo = [ar1++], ftw, wtw;
	
    ar2 = Vect;	
    ar4 = Temp;	
    //на вход Х рабочей матрицы подается Vect
    rep 1 data = [ar2] with vsum , data, 0;
    // результат операции выгружается из afifo в Temp
    rep 1 [ar4] = afifo;
	
    ar0,gr0 = [ar4];  // из памяти считывается пара значений 
    gr1 = ar0;        // над адресными регистрами нельзя совершать арифметические операции, поэтому содержимое ar0 копируется в gr1.
    // возвращаемое программой значение записывается в регистр gr7
    return with gr7 = gr1 + gr0;
end ".textAAA";       // признак окончания секции кода.
